plugins {
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'maven-publish'
    id 'com.palantir.docker' version '0.36.0'
    //id 'checkstyle'
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-amqp')

    implementation('com.fasterxml.jackson.core:jackson-databind')
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-xml')

    compileOnly('org.projectlombok:lombok:1.18.32')
    annotationProcessor('org.projectlombok:lombok:1.18.32')


    testCompileOnly('org.projectlombok:lombok:1.18.32')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.32')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('uk.org.webcompere:model-assert:1.0.0')


    test {
        useJUnitPlatform()
    }

}

ext {
    appName = 'doc-generator'
    appVersion = '0.0.1'
}

jar {
    enabled = false
}

bootJar {
    enabled = true
    archiveBaseName.set(appName)
    archiveVersion.set(appVersion)
}

publishing {
    publications {
        mavenJava(MavenPublication){
            artifact bootJar

            groupId = 'org.javaguru'
            artifactId = appName
            version = appVersion

            pom {
                name = 'Document Generator Application'
                description = 'Application to generate documentation'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }

}

docker {
    name "${appName}:${appVersion}"
    files tasks.bootJar.outputs
}

tasks.named("docker").configure {
    dependsOn tasks.bootJar
}

/*
checkstyle {
    configFile = 'config/checkstyle/checkstyle.xml' as File
}*/
