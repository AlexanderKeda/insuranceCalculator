plugins {
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'maven-publish'
    id 'com.palantir.docker' version '0.36.0'
    //id 'checkstyle'
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    implementation('org.springframework.boot:spring-boot-starter-amqp')

    implementation('mysql:mysql-connector-java:8.0.22')
    implementation('org.liquibase:liquibase-core')
    implementation('com.h2database:h2:2.2.224')

    implementation('com.google.guava:guava:32.1.1-jre')

    implementation('com.fasterxml.jackson.core:jackson-databind')
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-xml')

    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0')

    compileOnly('org.projectlombok:lombok:1.18.32')
    annotationProcessor('org.projectlombok:lombok:1.18.32')


    testCompileOnly('org.projectlombok:lombok:1.18.32')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.32')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('uk.org.webcompere:model-assert:1.0.0')


    test {
        useJUnitPlatform()
    }

}

ext {
    appName = 'insurance-calculator'
    appVersion = '1.0.3'
}

jar {
    enabled = false
}

bootJar {
    enabled = true
    archiveBaseName.set(appName)
    archiveVersion.set(appVersion)
}

publishing {
    publications {
        mavenJava(MavenPublication){
            artifact bootJar

            groupId = 'org.javaguru'
            artifactId = appName
            version = appVersion

            pom {
                name = 'Insurance Calculator Application'
                description = 'Application to calculate travel insurance premium'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }

}

docker {
    name "${appName}:${appVersion}"
    files tasks.bootJar.outputs
}

tasks.named("docker").configure {
    dependsOn tasks.bootJar
}

tasks.register("dockerComposeUp", Exec) {
    dependsOn tasks.docker
    commandLine  "docker", "compose", "up"
}

tasks.register("dockerComposeDown", Exec) {
    commandLine  "docker", "compose", "down"
}

/*
checkstyle {
    configFile = 'config/checkstyle/checkstyle.xml' as File
}*/
